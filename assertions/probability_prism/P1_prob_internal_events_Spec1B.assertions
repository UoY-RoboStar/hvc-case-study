package P1B

import basics::*

// P1B: That the actual system voltage always follows the set-point
pmodules M1B:
	// This module is used to output int_ActualHV as long as there is an internal int_ActualHV
	// from state_machine to the buffer simulating an asynchronous connection
	pmodule P1B_clear_ActualHV_buffer {
		send2buff: bool init true;
		
		[mod_sys::ctrl_ref0::stm_ref0::int_ActualHV.out] (@send2buff==true) -> (@send2buff=false);
		[mod_sys::rp_ref0::int_ActualHV.in] (@send2buff==false) -> (@send2buff=true);
	}
	
	pmodule P1B {
		// -1 - failure
		// 0 - idle
		// 1 & 2 - ErrorMode
		P1B_scpc : [-1 to 2] init 0;
		P1B_updating: bool init false;
		
		//////////////////////////////////// Idle state 0 (free events) ////////////////////////////////////////////
		//[mod_sys::rp_ref0::ext_setPoint.in]  (P1B_updating==false)/\(P1B_scpc==0)	-> (0.5:P1B_updating=false) & (0.5:P1B_updating=true); 
		[mod_sys::rp_ref0::ext_setPoint.out]  (@P1B_updating==false)/\(@P1B_scpc==0)	-> skip; 
		[mod_sys::rp_ref0::int_ActualHV.in] (@P1B_updating==false)/\(@P1B_scpc==0) -> skip;
		
	    [mod_sys::rp_ref0::currentState.in] (@P1B_updating==false) /\ ((@P1B_scpc >= 0) /\ (@P1B_scpc <= 2))            
	    	-> (@P1B_scpc=0)&(@P1B_updating=true);		
	    [] (@P1B_scpc==0)/\(@P1B_updating==true)/\
	    	(mod_sys::rp_ref0::currentState.in.val==State::ErrorMode) /\
	    	(@M1B::P1B_clear_ActualHV_buffer::send2buff==true)
	    	-> (@P1B_scpc=1)&(@P1B_updating=false);
	    // if send2buff==false, we know prior to this state, a int_ActualHV is waiting for output, 
	    // and so we synchronise on it to clear the buffer to make sure there is no pending int_ActualHV,
	    // otherwise, int_ActualHV won't follow setPoint correctly
	    [mod_sys::rp_ref0::int_ActualHV.in] (@P1B_scpc==0)/\(@P1B_updating==true)/\
	    	(mod_sys::rp_ref0::currentState.in.val==State::ErrorMode) /\
	    	(@M1B::P1B_clear_ActualHV_buffer::send2buff==false)
	    	-> (@P1B_scpc=1)&(@P1B_updating=false);
	    // otherwise, return to idle by setting scpc to 0
	    [] (@P1B_scpc==0)/\(@P1B_updating==true) /\(mod_sys::rp_ref0::currentState.in.val!=State::ErrorMode) 
	    	-> (@P1B_scpc=0)&(@P1B_updating=false);
	    
	    /////////////////////////////////////////////// State 1  //////////////////////////////////////////////////
	    [mod_sys::rp_ref0::ext_setPoint.in]   (@P1B_updating==false)/\(@P1B_scpc==1)                      
	    	-> (@P1B_scpc=1)&(@P1B_updating=true);

	    [] (@P1B_scpc==1)/\(@P1B_updating==true)/\(mod_sys::rp_ref0::ext_setPoint.in.val==0)             
	    	-> (@P1B_scpc=2)&(@P1B_updating=false);
	    	
	    [] (@P1B_scpc==1)/\(@P1B_updating==true)/\(mod_sys::rp_ref0::ext_setPoint.in.val!=0)             
	    	-> (@P1B_scpc=-1)&(@P1B_updating=false);
	    	
	    /////////////////////////////////////////////// State 2  //////////////////////////////////////////////////
	    [mod_sys::rp_ref0::int_ActualHV.in]  (@P1B_updating==false)/\(@P1B_scpc==2)                    
	    	-> (@P1B_scpc=2)&(@P1B_updating=true);
	    
	    [] (@P1B_scpc==2)/\(@P1B_updating==true)/\(mod_sys::rp_ref0::int_ActualHV.in.val==0)             
	    	-> (@P1B_scpc=1)&(@P1B_updating=false);
	    	
	    [] (@P1B_scpc==2)/\(@P1B_updating==true)/\(mod_sys::rp_ref0::int_ActualHV.in.val!=0)             
	    	-> (@P1B_scpc=-1)&(@P1B_updating=false);
	    	
	}
	
label l1 = @M1B::P1B::P1B_scpc != -1

prob property P1B_deadlock_free:
	not Exists [Finally deadlock]
	with definitions D1
	with modules M1B
	with cmdoptions "-hybrid -cuddmaxmem 400g -javamaxmem 8g"
	
prob property P1B:
	Forall [Globally #l1]
	with definitions D1
	with modules M1B
	with cmdoptions "-hybrid -cuddmaxmem 400g -javamaxmem 8g"
